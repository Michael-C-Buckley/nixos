# GTK Appearance Module
#  NOTE: Cursors are handled on their on in `../cursor`
{
  config,
  lib,
  user,
  ...
}: let
  inherit (lib) mkOption;
  inherit (lib.types) package lines str attrs;

  userCfg = config.home.features.${user};
  local = userCfg.gtk;
in {
  options.home.features.${user}.gtk = {
    theme = {
      name = mkOption {
        type = str;
        description = "Name of the GTK theme to use.";
      };
      package = mkOption {
        type = package;
        description = "User's GTK theme package.";
      };
    };
    iconTheme = {
      name = mkOption {
        type = str;
        description = "Name of the GTK icon theme theme to use.";
      };
      package = mkOption {
        type = package;
        description = "User's GTK icon theme package.";
      };
    };

    # Info that will be implemented later via various methods
    #  including hjem versus home-manager
    #  as well as squashing options into their final file, like GTK
    _info = mkOption {
      type = lines;
      internal = true;
      description = "Generated GTK cursor settings.";
      default = ''
        gtk-theme-name=${local.theme.name}
        gtk-icon-theme-name=${local.iconTheme.name}
      '';
    };
    # The GTK ini file builder
    _file = mkOption {
      type = lines;
      internal = true;
      description = "Generated GTK file lines ready for linking.";
      default = ''
        # Generated by Nix
        [settings]
        ${local._info}
        ${userCfg.cursor._info.gtk}
      '';
    };
    # The prepared attrset for GTK theme items
    _files = mkOption {
      type = attrs;
      internal = true;
      description = "Set of files ready to be linked.";
      default = {
        ".config/gtk-3.0/settings.ini".text = userCfg.gtk._file;
        ".config/gtk-4.0/settings.ini".text = userCfg.gtk._file;
      };
    };
  };
  config = {
    users.users.${user}.packages = [
      local.theme.package
      local.iconTheme.package
    ];
  };
}
